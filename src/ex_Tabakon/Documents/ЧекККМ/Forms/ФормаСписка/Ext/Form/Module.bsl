
&НаКлиенте
Процедура ТБКВозвратОтПокупателяПосле(Команда) 	
	ЧекККМ = Элементы.Список.ТекущаяСтрока;
	
	Если ЗначениеЗаполнено(ЧекККМ) тогда
		Если НачалоДня(Элементы.Список.ТекущиеДанные.Дата) = НачалоДня(ТекущаяДата()) тогда
			Сообщить("Для возврата чека за сегодняшний день - воспользуйтесь РМК!");
			//Возврат;
		КонецЕсли;
		
		ЭтоПродажа = ПолучитьЭтоПродажа(ЧекККМ);
		
		Если ЭтоПродажа тогда
			Если не ОбщегоНазначенияВызовСервера.ПроверитьЧекНаНаличиеСкидок(ЧекККМ) тогда
				Сообщить("Возврат чека не возможен! В выбранном чеке присутсвует уцененный товар!");
				Возврат;	
			КонецЕсли;
			
			НовыйДокументВозврата = ЗаполнитьДокументВозвратТоваровОтПокупателяНаСервере(ЧекККМ);
			
			Если НовыйДокументВозврата<> Неопределено тогда
				ПараметрыОткрытия = Новый Структура("Ключ",НовыйДокументВозврата);
				ОткрытьФорму("Документ.ВозвратТоваровОтПокупателя.Форма.ФормаДокумента",ПараметрыОткрытия);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли; 	
КонецПроцедуры

&НаСервереБезКонтекста 
Функция ПолучитьЭтоПродажа(ЧекККМ)
	Возврат ЧекККМ.ВидОперации = Перечисления.ВидыОперацийЧекККМ.Продажа;
КонецФункции


&НаСервереБезКонтекста
Функция ЗаполнитьДокументВозвратТоваровОтПокупателяНаСервере(ЧекККМ)
	НовыйДокументВозврата = Документы.ВозвратТоваровОтПокупателя.СоздатьДокумент();   	
	
	ЗаполнитьЗначенияСвойств(НовыйДокументВозврата,ЧекККМ);
	
	НовыйДокументВозврата.УстановитьНовыйНомер();
	НовыйДокументВозврата.Дата = ТекущаяДата();
	НовыйДокументВозврата.Склад = ЧекККМ.Магазин.СкладПродажи;
	НовыйДокументВозврата.Контрагент = Справочники.Контрагенты.НайтиПоНаименованию(СокрЛП(ЧекККМ.Магазин.Наименование)+" "+"Покупатель");
	
	Для каждого Строка из ЧекККМ.Товары цикл
		НоваяСтрока = НовыйДокументВозврата.Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Строка);  
			
		НоваяСтрока.ДокументПродажи	=	ЧекККМ.ОтчетОРозничныхПродажах;
		НоваяСтрока.ЧекККМ			=	ЧекККМ; 
	КонецЦикла;
	
	ЗаполнитьКоличествоБаллов(ЧекККМ,НовыйДокументВозврата);
	
	//Дополнительный блок
	НовыйДокументВозврата.Товары.Свернуть("Номенклатура,Характеристика,Упаковка,Цена,СтавкаНДС,ДокументПродажи,Продавец,ЧекККМ,СтатусУказанияСерий,Штрихкод,НеобходимостьВводаАкцизнойМарки",
											"Количество,КоличествоУпаковок,Сумма,СуммаНДС,СуммаСкидкиОплатыБонусом,СуммаАвтоматическойСкидки,СуммаРучнойСкидки,ТБК_КоличествоБаллов");
	
	УжеВозвращено = ПолучитьТЗУжеВозвращено(ЧекККМ);
	Для каждого Строка из УжеВозвращено цикл
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Номенклатура",Строка.Номенклатура);
		ПараметрыОтбора.Вставить("Характеристика",Строка.Характеристика);
		
		СтрокиДокумента = НовыйДокументВозврата.Товары.НайтиСтроки(ПараметрыОтбора); 		
		Если СтрокиДокумента.Количество() = 1 тогда	
			Для каждого СтрокаДокумента из СтрокиДокумента цикл
				СтрокаДокумента.Количество 					= СтрокаДокумента.Количество - Строка.Количество;
				СтрокаДокумента.КоличествоУпаковок 			= СтрокаДокумента.КоличествоУпаковок - Строка.КоличествоУпаковок;
				СтрокаДокумента.Сумма 						= СтрокаДокумента.Сумма - Строка.Сумма;
				СтрокаДокумента.СуммаНДС 					= СтрокаДокумента.СуммаНДС - Строка.СуммаНДС;
				СтрокаДокумента.СуммаСкидкиОплатыБонусом 	= СтрокаДокумента.СуммаСкидкиОплатыБонусом - Строка.СуммаСкидкиОплатыБонусом;
				СтрокаДокумента.СуммаАвтоматическойСкидки 	= СтрокаДокумента.СуммаАвтоматическойСкидки - Строка.СуммаАвтоматическойСкидки;
				СтрокаДокумента.СуммаРучнойСкидки 			= СтрокаДокумента.СуммаРучнойСкидки - Строка.СуммаРучнойСкидки;
			КонецЦикла;
		КонецЕсли; 		
	КонецЦикла;
	НулевыеСтроки = НовыйДокументВозврата.Товары.НайтиСтроки(Новый Структура("КоличествоУпаковок",0));
	Для каждого СтрокаТаблицы Из НулевыеСтроки Цикл
		НовыйДокументВозврата.Товары.Удалить(СтрокаТаблицы)
	КонецЦикла; 
	
	Если НовыйДокументВозврата.Товары.Количество() = 0 тогда
		ТекстСообщения = "По данному чеку все товары возвращены!";
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат Неопределено;
	КонецЕсли;
	//
	
	Отказ = Ложь;
	
	ПродажиСервер.ПроверитьВозможностьВозвратаОтПокупателя(
	НовыйДокументВозврата,
	Отказ);
	
	Если не Отказ тогда
		НовыйДокументВозврата.Записать(РежимЗаписиДокумента.Проведение); 
		Возврат НовыйДокументВозврата.Ссылка;
	иначе
		Возврат Неопределено;
	КонецЕсли;

КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьТЗУжеВозвращено(ЧекККМ)
	Запрос	=	Новый запрос("ВЫБРАТЬ
	      	 	             |	ВозвратТоваровОтПокупателяТовары.Номенклатура КАК Номенклатура,
	      	 	             |	ВозвратТоваровОтПокупателяТовары.Характеристика КАК Характеристика,
	      	 	             |	СУММА(ВозвратТоваровОтПокупателяТовары.Количество) КАК Количество,
	      	 	             |	СУММА(ВозвратТоваровОтПокупателяТовары.КоличествоУпаковок) КАК КоличествоУпаковок,
	      	 	             |	СУММА(ВозвратТоваровОтПокупателяТовары.Сумма) КАК Сумма,
	      	 	             |	СУММА(ВозвратТоваровОтПокупателяТовары.СуммаНДС) КАК СуммаНДС,
	      	 	             |	СУММА(ВозвратТоваровОтПокупателяТовары.СуммаСкидкиОплатыБонусом) КАК СуммаСкидкиОплатыБонусом,
	      	 	             |	СУММА(ВозвратТоваровОтПокупателяТовары.СуммаАвтоматическойСкидки) КАК СуммаАвтоматическойСкидки,
	      	 	             |	СУММА(ВозвратТоваровОтПокупателяТовары.СуммаРучнойСкидки) КАК СуммаРучнойСкидки
	      	 	             |ИЗ
	      	 	             |	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
	      	 	             |ГДЕ
	      	 	             |	ВозвратТоваровОтПокупателяТовары.ЧекККМ = &ЧекККМ
	      	 	             |	И ВозвратТоваровОтПокупателяТовары.Ссылка.Проведен
	      	 	             |
	      	 	             |СГРУППИРОВАТЬ ПО
	      	 	             |	ВозвратТоваровОтПокупателяТовары.Номенклатура,
	      	 	             |	ВозвратТоваровОтПокупателяТовары.Характеристика");
	Запрос.УстановитьПараметр("ЧекККМ",ЧекККМ);
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаполнитьКоличествоБаллов(ЧекККМ, НовыйДокументВозврата)  		
	Запрос	=	Новый запрос("ВЫБРАТЬ
	      	 	             |	ТБК_ИсторияПокупокРозница.Чек КАК Чек,
	      	 	             |	ТБК_ИсторияПокупокРозница.IDКлиента КАК IDКлиента,
	      	 	             |	ТБК_ИсторияПокупокРозница.Товары КАК Товары
	      	 	             |ИЗ
	      	 	             |	РегистрСведений.ТБК_ИсторияПокупокРозница КАК ТБК_ИсторияПокупокРозница
	      	 	             |ГДЕ
	      	 	             |	ТБК_ИсторияПокупокРозница.Чек = &Чек");
	Запрос.УстановитьПараметр("Чек",ЧекККМ);
	Рез = Запрос.Выполнить().Выгрузить();
	
	СтруктураВозврата	=	Новый Структура;
	
	Если Рез.Количество()>0 тогда
		ТЗТовары	=	Рез[0].Товары.получить();
		
		Для каждого Строка из НовыйДокументВозврата.Товары цикл
			Строка.ТБК_КоличествоБаллов	=	-1*ТЗТовары[Строка.НомерСтроки-1].ТБК_КоличествоБаллов;
		КонецЦикла;  
	КонецЕсли; 
КонецПроцедуры

