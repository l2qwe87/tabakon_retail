
&НаКлиенте
Процедура ВыгрузитьВФайл(Команда)	
	
	
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	Диалог.Заголовок = НСтр("ru='Выберите файл, куда записать продажи'");
	Диалог.МножественныйВыбор = Ложь;
	Диалог.Расширение = "xml";
	Диалог.Фильтр = НСтр("ru='JSON (*.json)|*.json'");

	ДополнительныеПараметры = Новый Структура;
	ОписаниеОповещенияПослеВыбора = Новый ОписаниеОповещения("Обработчик_Выгрузки", ЭтаФорма, ДополнительныеПараметры);

	Диалог.Показать(ОписаниеОповещенияПослеВыбора);

		
КонецПроцедуры

&НаКлиенте
Процедура Обработчик_Выгрузки(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт 

	ТипМассив = Тип("Массив");

	Если (ТипЗнч(ВыбранныеФайлы) = ТипМассив)
			И (ВыбранныеФайлы.Количество() > 0) Тогда

		ИмяФайла = ВыбранныеФайлы[0];
		Если НЕ ПустаяСтрока(ИмяФайла) Тогда
			
			продажиЗаПериод = ВыгрузитьВФайлНаСервере(
				ЭтаФорма.ПериодВыгрузки.ДатаНачала, 
				ЭтаФорма.ПериодВыгрузки.ДатаОкончания
			);
			
			ЗаписьJSON = Новый ЗаписьJSON;
			ЗаписьJSON.ОткрытьФайл(имяФайла);
			ЗаписатьJSON(ЗаписьJSON,продажиЗаПериод);
			ЗаписьJSON.Закрыть();
			
			Сообщить("Файл сформирован: "+ИмяФайла)
		КонецЕсли;
	КонецЕсли;
 КонецПроцедуры

&НаСервереБезКонтекста
Функция ВыгрузитьВФайлНаСервере(датаН, датаК)
	// Вставить содержимое обработчика.
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	Продажи.Номенклатура КАК Номенклатура,
	               |	Продажи.Характеристика КАК Характеристика,
	               |	МАКСИМУМ(Продажи.Цена) КАК Цена,
	               |	СУММА(Продажи.Количество) КАК Количество
	               |ИЗ
	               |	Документ.ЧекККМ.Товары КАК Продажи
	               |ГДЕ
	               |	Продажи.Ссылка.Проведен
	               |	И Продажи.Ссылка.СтатусЧекаККМ В(&СтатусыЧекаККМ)
	               |	И Продажи.Ссылка.Дата >= &датаН
	               |	И Продажи.Ссылка.Дата <= &датаК
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	Продажи.Номенклатура,
	               |	Продажи.Характеристика";
	
	
	СтатусыЧекаККМ = Новый Массив;
	СтатусыЧекаККМ.Добавить(Перечисления.СтатусыЧековККМ.Архивный);
	СтатусыЧекаККМ.Добавить(Перечисления.СтатусыЧековККМ.Пробитый);
	
	Запрос.УстановитьПараметр("СтатусыЧекаККМ",СтатусыЧекаККМ);
	Запрос.УстановитьПараметр("датаН",НачалоДня(датаН));
	Запрос.УстановитьПараметр("датаК",КонецДня(датаК));
	
	рез = Запрос.Выполнить().Выбрать();
	
	результ = Новый Массив;
	Пока рез.Следующий() Цикл
		результ.Добавить(
			Новый Структура("НоменклатураУИ,Номенклатура,ХарактеристикаУИ,Характеристика,Количество,Цена",
				Строка(рез.Номенклатура.УникальныйИдентификатор()),
				Строка(рез.Номенклатура),
				?(рез.Характеристика=неопределено,"",Строка(рез.Характеристика.УникальныйИдентификатор())),
				?(рез.Характеристика=неопределено,"",Строка(рез.Характеристика)),
				рез.Количество,
				рез.Цена
			)
		);
	КонецЦикла;
	
	Возврат результ;
КонецФункции
