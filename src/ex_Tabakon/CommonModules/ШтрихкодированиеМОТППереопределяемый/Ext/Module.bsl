
&Вместо("ПриОпределенииОстатковМаркируемойПродукции")
Процедура ТБКПриОпределенииОстатковМаркируемойПродукции(ОстаткиМаркируемойПродукции, ПараметрыСканирования)
			Если ОстаткиМаркируемойПродукции.Колонки.Найти("БылиПродажи") = Неопределено Тогда
		ОстаткиМаркируемойПродукции.Колонки.Добавить("БылиПродажи", Новый ОписаниеТипов("Булево"));
	КонецЕсли;
	
	Если ОстаткиМаркируемойПродукции.Колонки.Найти("БылиВозвраты") = Неопределено Тогда
		ОстаткиМаркируемойПродукции.Колонки.Добавить("БылиВозвраты", Новый ОписаниеТипов("Булево"));
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МаркируемаяПродукция.ШтрихкодУпаковки КАК ШтрихкодУпаковки,
	|	МаркируемаяПродукция.Доступно КАК Доступно,
	|	МаркируемаяПродукция.БылиПродажи КАК БылиПродажи,
	|	МаркируемаяПродукция.БылиВозвраты КАК БылиВозвраты
	|ПОМЕСТИТЬ ВТМаркируемаяПродукция
	|ИЗ
	|	&МаркируемаяПродукция КАК МаркируемаяПродукция
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ШтрихкодУпаковки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЧекККМ.Ссылка КАК ЧекККМ
	|ПОМЕСТИТЬ ЧекиККМПродажа
	|ИЗ
	|	Документ.ЧекККМ КАК ЧекККМ
	|ГДЕ
	|	ЧекККМ.Организация = &Организация
	|	И ЧекККМ.Проведен
	|	И ЧекККМ.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Продажа)
	|	И ЧекККМ.Дата >= &Дата
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЧекККМ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЧекККМ.Ссылка КАК ЧекККМ
	|ПОМЕСТИТЬ ЧекиККМВозврат
	|ИЗ
	|	Документ.ЧекККМ КАК ЧекККМ
	|ГДЕ
	|	ЧекККМ.Организация = &Организация
	|	И ЧекККМ.Проведен
	|	И ЧекККМ.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЧекККМ.Возврат)
	|	И ЧекККМ.Дата >= &Дата
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ЧекККМ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОтчетыОРозничныхПродажах.Ссылка КАК ОРП
	|ПОМЕСТИТЬ ОтчетыОРозничныхПродажах
	|ИЗ
	|	Документ.ОтчетОРозничныхПродажах КАК ОтчетыОРозничныхПродажах
	|ГДЕ
	|	ОтчетыОРозничныхПродажах.Организация = &Организация
	|	И ОтчетыОРозничныхПродажах.Проведен
	|	И ОтчетыОРозничныхПродажах.Дата >= &Дата
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ОРП
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЧекККМАкцизныеМарки.АкцизнаяМарка КАК ШтрихкодУпаковки,
	|	-1 КАК Доступно,
	|	ИСТИНА КАК БылиПродажи,
	|	ЛОЖЬ КАК БылиВозвраты
	|ПОМЕСТИТЬ ДанныеДокументов
	|ИЗ
	|	ВТМаркируемаяПродукция КАК ВТМаркируемаяПродукция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЧекККМ.АкцизныеМарки КАК ЧекККМАкцизныеМарки
	|		ПО ВТМаркируемаяПродукция.ШтрихкодУпаковки = ЧекККМАкцизныеМарки.АкцизнаяМарка
	|ГДЕ
	|	ЧекККМАкцизныеМарки.Ссылка В
	|			(ВЫБРАТЬ
	|				ЧекиПродажи.ЧекККМ
	|			ИЗ
	|				ЧекиККМПродажа КАК ЧекиПродажи)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЧекККМАкцизныеМарки.АкцизнаяМарка,
	|	1,
	|	ЛОЖЬ,
	|	ИСТИНА
	|ИЗ
	|	ВТМаркируемаяПродукция КАК ВТМаркируемаяПродукция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЧекККМ.АкцизныеМарки КАК ЧекККМАкцизныеМарки
	|		ПО ВТМаркируемаяПродукция.ШтрихкодУпаковки = ЧекККМАкцизныеМарки.АкцизнаяМарка
	|ГДЕ
	|	ЧекККМАкцизныеМарки.Ссылка В
	|			(ВЫБРАТЬ
	|				ЧекиВозврат.ЧекККМ
	|			ИЗ
	|				ЧекиККМВозврат КАК ЧекиВозврат)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ОтчетОРозничныхПродажахАкцизныеМарки.АкцизнаяМарка,
	|	-1,
	|	ИСТИНА,
	|	ЛОЖЬ
	|ИЗ
	|	ВТМаркируемаяПродукция КАК ВТМаркируемаяПродукция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах.АкцизныеМарки КАК ОтчетОРозничныхПродажахАкцизныеМарки
	|		ПО ВТМаркируемаяПродукция.ШтрихкодУпаковки = ОтчетОРозничныхПродажахАкцизныеМарки.АкцизнаяМарка
	|ГДЕ
	|	ОтчетОРозничныхПродажахАкцизныеМарки.Ссылка В
	|			(ВЫБРАТЬ
	|				ОтчетыОРозничныхПродажах.ОРП
	|			ИЗ
	|				ОтчетыОРозничныхПродажах КАК ОтчетыОРозничныхПродажах)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВозвратТоваровОтПокупателяШтрихкодыУпаковок.ШтрихкодУпаковки,
	|	1,
	|	ЛОЖЬ,
	|	ИСТИНА
	|ИЗ
	|	ВТМаркируемаяПродукция КАК ВТМаркируемаяПродукция
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтПокупателя.ШтрихкодыУпаковок КАК ВозвратТоваровОтПокупателяШтрихкодыУпаковок
	|		ПО ВТМаркируемаяПродукция.ШтрихкодУпаковки = ВозвратТоваровОтПокупателяШтрихкодыУпаковок.ШтрихкодУпаковки
	|ГДЕ
	|	ВозвратТоваровОтПокупателяШтрихкодыУпаковок.Ссылка.Организация = &Организация
	|	И ВозвратТоваровОтПокупателяШтрихкодыУпаковок.Ссылка.Проведен
	|	И ВозвратТоваровОтПокупателяШтрихкодыУпаковок.Ссылка.Дата >= &Дата
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТМаркируемаяПродукция.ШтрихкодУпаковки,
	|	ВТМаркируемаяПродукция.Доступно,
	|	ВТМаркируемаяПродукция.БылиПродажи,
	|	ВТМаркируемаяПродукция.БылиВозвраты
	|ИЗ
	|	ВТМаркируемаяПродукция КАК ВТМаркируемаяПродукция
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДокументов.ШтрихкодУпаковки КАК ШтрихкодУпаковки,
	|	СУММА(ДанныеДокументов.Доступно) + 1 КАК Доступно,
	|	МАКСИМУМ(ДанныеДокументов.БылиПродажи) КАК БылиПродажи,
	|	МАКСИМУМ(ДанныеДокументов.БылиВозвраты) КАК БылиВозвраты
	|ИЗ
	|	ДанныеДокументов КАК ДанныеДокументов
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДокументов.ШтрихкодУпаковки";
	
	Запрос.УстановитьПараметр("Организация",          ПараметрыСканирования.Организация);
	Запрос.УстановитьПараметр("МаркируемаяПродукция", ОстаткиМаркируемойПродукции);
	Запрос.УстановитьПараметр("Дата", ТекущаяДата() - 30*24*60*60); //Вик 20210506 Проверяем данные за 30 дней

	
	ОстаткиМаркируемойПродукции = Запрос.Выполнить().Выгрузить();

КонецПроцедуры
