
&Вместо("ПрименитьРезультатРасчетаСкидокКОбъекту")
Процедура ТБКПрименитьРезультатРасчетаСкидокКОбъекту(Объект, ИмяТЧ, РезультатРасчетаСкидокНаценок, ЕстьБонусы)
	Если ТипЗнч(Объект.СкидкиНаценки) = Тип("ТаблицаЗначений") Тогда
		Объект.СкидкиНаценки = РезультатРасчетаСкидокНаценок.Скопировать();
	Иначе
		Объект.СкидкиНаценки.Загрузить(РезультатРасчетаСкидокНаценок);
	КонецЕсли;
	АвтоматическиеСкидкиНаценки = РезультатРасчетаСкидокНаценок.Скопировать();
	
	// Заполнение скидок в табличной части "Товары".
	АвтоматическиеСкидкиНаценки.Свернуть("КлючСвязи", "Сумма");
	АвтоматическиеСкидкиНаценки.Индексы.Добавить("КлючСвязи");
	
	Для Каждого СтрокаТЧ Из Объект[ИмяТЧ] Цикл
		
		СтрокаТаблицы = АвтоматическиеСкидкиНаценки.Найти(СтрокаТЧ.КлючСвязи, "КлючСвязи");
		Если СтрокаТаблицы = Неопределено Тогда
			СтрокаТЧ.СуммаАвтоматическойСкидки = 0;
			СуммаАвтоматическойСкидки          = 0; // Для точного расчета процента автоматической скидки.
		Иначе
			СтрокаТЧ.СуммаАвтоматическойСкидки = СтрокаТаблицы.Сумма;
			СуммаАвтоматическойСкидки          = СтрокаТаблицы.Сумма; // Для точного расчета процента автоматической скидки.
		КонецЕсли;
		
		// Применение автоматической скидки.
		СуммаБезСкидки = СтрокаТЧ.КоличествоУпаковок * СтрокаТЧ.Цена;
		
		//Если СуммаБезСкидки <> 0 Тогда 
		Попытка
			Если СуммаБезСкидки <> 0 и не СтрокаТЧ.флПроставленаСкидкаПоАкции Тогда
				СтрокаТЧ.ПроцентРучнойСкидки = 100 * СтрокаТЧ.СуммаРучнойСкидки / СуммаБезСкидки;
			КонецЕсли;
		Исключение
		КонецПопытки;
		
		СуммаСкидки = СуммаАвтоматическойСкидки + СтрокаТЧ.СуммаРучнойСкидки + ?(ЕстьБонусы, СтрокаТЧ.СуммаСкидкиОплатыБонусом, 0);
		
		СтрокаТЧ.ПроцентАвтоматическойСкидки = ?(СуммаБезСкидки = 0, 0 , 100 * СуммаАвтоматическойСкидки / СуммаБезСкидки);
		
		СтрокаТЧ.Сумма    = СуммаБезСкидки - ?(СуммаСкидки > СуммаБезСкидки, СуммаБезСкидки, СуммаСкидки);
		СтрокаТЧ.СуммаНДС = РассчитатьСуммуНДС(СтрокаТЧ, Объект.ЦенаВключаетНДС);
		
	КонецЦикла;
	
	Объект.СуммаДокумента = ОбработкаТабличнойЧастиТоварыКлиентСервер.СуммаДокумента(Объект[ИмяТЧ], Объект.ЦенаВключаетНДС);
	Объект.СкидкиРассчитаны = Истина;

КонецПроцедуры

&Вместо("НазначитьРучнуюСкидку")
Процедура ТБКНазначитьРучнуюСкидку(Объект, ИмяТЧ, Знач СуммаСкидкиКРаспределению, ИспользуютсяАвтоматическиеСкидки, ЕстьБонусы)
	МаксимальнаяСуммаСкидки = 0;
	Для Каждого СтрокаТЧ Из Объект[ИмяТЧ] Цикл
		
		Если СтрокаТЧ.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.ПодарочныйСертификат
			ИЛИ (СтрокаТЧ.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга
					И ЗначениеЗаполнено(СтрокаТЧ.ДоговорКонтрагента)) Тогда
			Продолжить;
		КонецЕсли;
		СуммаАвтоматическойСкидки = ?(ИспользуютсяАвтоматическиеСкидки, СтрокаТЧ.СуммаАвтоматическойСкидки, 0);
		Если ЕстьБонусы Тогда
			СуммаАвтоматическойСкидки = СуммаАвтоматическойСкидки + СтрокаТЧ.СуммаСкидкиОплатыБонусом;
		КонецЕсли;
		МаксимальнаяСуммаСкидки = МаксимальнаяСуммаСкидки + СтрокаТЧ.КоличествоУпаковок * СтрокаТЧ.Цена - СуммаАвтоматическойСкидки;
		
	КонецЦикла;
	
	Если СуммаСкидкиКРаспределению > МаксимальнаяСуммаСкидки Тогда
		СуммаСкидкиКРаспределению = МаксимальнаяСуммаСкидки;
	КонецЕсли;
	
	Для каждого СтрокаТЧ Из Объект[ИмяТЧ] Цикл
		
		Если СтрокаТЧ.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.ПодарочныйСертификат
			ИЛИ (СтрокаТЧ.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.Услуга
					И ЗначениеЗаполнено(СтрокаТЧ.ДоговорКонтрагента)) Тогда
			Продолжить;
		КонецЕсли;
		
		СуммаБезСкидки = СтрокаТЧ.КоличествоУпаковок * СтрокаТЧ.Цена;
		СуммаАвтоматическойСкидки = ?(ИспользуютсяАвтоматическиеСкидки, СтрокаТЧ.СуммаАвтоматическойСкидки, 0);
		Если ЕстьБонусы Тогда
			СуммаАвтоматическойСкидки = СуммаАвтоматическойСкидки + СтрокаТЧ.СуммаСкидкиОплатыБонусом;
		КонецЕсли;
		
		Если МаксимальнаяСуммаСкидки <> 0 Тогда
			СтрокаТЧ.СуммаРучнойСкидки = (СуммаБезСкидки - СуммаАвтоматическойСкидки) * (СуммаСкидкиКРаспределению / МаксимальнаяСуммаСкидки);
			//Марк 2024-03-13
			СтрокаТЧ.РучнаяСкидка = (СуммаБезСкидки - СуммаАвтоматическойСкидки) * (СуммаСкидкиКРаспределению / МаксимальнаяСуммаСкидки);
			//КонецМарк
		Иначе
			СтрокаТЧ.СуммаРучнойСкидки = 0;
			//Марк 2024-03-13
			СтрокаТЧ.РучнаяСкидка = 0;
			//КонецМарк
		КонецЕсли;
		СуммаСкидкиКРаспределению = СуммаСкидкиКРаспределению - СтрокаТЧ.СуммаРучнойСкидки;
		МаксимальнаяСуммаСкидки = МаксимальнаяСуммаСкидки - (СуммаБезСкидки - СуммаАвтоматическойСкидки);
		
		Если СуммаБезСкидки <> 0 Тогда
			СтрокаТЧ.ПроцентРучнойСкидки = 100 * СтрокаТЧ.СуммаРучнойСкидки / СуммаБезСкидки;
		Иначе
			СтрокаТЧ.ПроцентРучнойСкидки = 0;
		КонецЕсли;
		
		СуммаСкидки = СуммаАвтоматическойСкидки + СтрокаТЧ.СуммаРучнойСкидки;
		
		Если ИспользуютсяАвтоматическиеСкидки Тогда
			Если СуммаБезСкидки <> 0 Тогда
				СтрокаТЧ.ПроцентАвтоматическойСкидки = ?(СуммаБезСкидки = 0, 0 , 100 * СуммаАвтоматическойСкидки / СуммаБезСкидки);
			Иначе
				СтрокаТЧ.ПроцентАвтоматическойСкидки = 0;
			КонецЕсли;
		КонецЕсли;
		
		СтрокаТЧ.Сумма    = СуммаБезСкидки - ?(СуммаСкидки > СуммаБезСкидки, СуммаБезСкидки, СуммаСкидки);
		СтрокаТЧ.СуммаНДС = РассчитатьСуммуНДС(СтрокаТЧ, Объект.ЦенаВключаетНДС);
		
	КонецЦикла;
	
	Объект.СуммаДокумента = ОбработкаТабличнойЧастиТоварыКлиентСервер.СуммаДокумента(Объект[ИмяТЧ], Объект.ЦенаВключаетНДС);

	//ПродолжитьВызов(Объект, ИмяТЧ, СуммаСкидкиКРаспределению, ИспользуютсяАвтоматическиеСкидки, ЕстьБонусы);
КонецПроцедуры

&Вместо("ОтменитьРучныеСкидки")
Процедура ТБКОтменитьРучныеСкидки(Объект, ИмяТЧ, ИспользуютсяАвтоматическиеСкидки, ЕстьБонусы)
	Для Каждого СтрокаТЧ Из Объект[ИмяТЧ] Цикл
		
		СтрокаТЧ.СуммаРучнойСкидки   = 0;
		СтрокаТЧ.ПроцентРучнойСкидки = 0;
		//Марк 2024-03-14
		СтрокаТЧ.РучнаяСкидка 		 = 0;
		//КонецМарк
		
		СуммаБезСкидки = СтрокаТЧ.КоличествоУпаковок * СтрокаТЧ.Цена;
		
		СуммаАвтоматическойСкидки = ?(ИспользуютсяАвтоматическиеСкидки, СтрокаТЧ.СуммаАвтоматическойСкидки, 0);
		Если ЕстьБонусы Тогда
			СуммаАвтоматическойСкидки = СуммаАвтоматическойСкидки + СтрокаТЧ.СуммаСкидкиОплатыБонусом;
		КонецЕсли;
		
		СтрокаТЧ.Сумма    = СуммаБезСкидки - ?(СуммаАвтоматическойСкидки > СуммаБезСкидки, СуммаБезСкидки, СуммаАвтоматическойСкидки);
		СтрокаТЧ.СуммаНДС = РассчитатьСуммуНДС(СтрокаТЧ, Объект.ЦенаВключаетНДС);
		
	КонецЦикла;
	
	Объект.СуммаДокумента = ОбработкаТабличнойЧастиТоварыКлиентСервер.СуммаДокумента(Объект[ИмяТЧ], Объект.ЦенаВключаетНДС);
	//ПродолжитьВызов(Объект, ИмяТЧ, ИспользуютсяАвтоматическиеСкидки, ЕстьБонусы);
КонецПроцедуры
