Процедура ЗаполнитьОстаткиНоменклатурыБезУчетаХарактеристик(Объект, ИмяТабличнойЧасти) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаНоменклатуры.Номенклатура,
	|	ТаблицаНоменклатуры.Склад
	|ПОМЕСТИТЬ ТаблицаВЗапрос
	|ИЗ
	|	&ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТоварыНаСкладахОстатки.Склад,
	|	ТоварыНаСкладахОстатки.Номенклатура,
	|	ТоварыНаСкладахОстатки.КоличествоОстаток - ТоварыНаСкладахОстатки.РезервОстаток КАК ОстатокБезРезерва
	|ИЗ
	|	РегистрНакопления.ТоварыНаСкладах.Остатки(
	|			,
	|			(Номенклатура,  Склад) В
	|				(ВЫБРАТЬ
	|					ТаблицаВЗапрос.Номенклатура,
	|					ТаблицаВЗапрос.Склад
	|				ИЗ
	|					ТаблицаВЗапрос КАК ТаблицаВЗапрос)) КАК ТоварыНаСкладахОстатки";
	
	ТаблицаНоменклатуры = Объект[ИмяТабличнойЧасти].Выгрузить();
	
	Запрос.УстановитьПараметр("ТаблицаНоменклатуры", ТаблицаНоменклатуры);
	
	Результат = Запрос.Выполнить();
	ТаблицаОстатков = Результат.Выгрузить();
	
	СтруктураПоиска = Новый Структура;
	МассивОстатков = Новый Массив;
	
	Для каждого СтрокаТаблицыНоменклатуры Из Объект[ИмяТабличнойЧасти] Цикл
		Если не СтрокаТаблицыНоменклатуры.ХарактеристикиИспользуются тогда
	
			СтруктураПоиска.Вставить("Номенклатура"  , СтрокаТаблицыНоменклатуры.Номенклатура);
			СтруктураПоиска.Вставить("Склад"         , СтрокаТаблицыНоменклатуры.Склад);
			
			МассивСтрок = ТаблицаОстатков.НайтиСтроки(СтруктураПоиска);
			
			Если МассивСтрок.Количество() > 0  Тогда
				СтрокаТаблицыНоменклатуры.Остаток = МассивСтрок[0].ОстатокБезРезерва;
				МассивСтрок[0].ОстатокБезРезерва = МассивСтрок[0].ОстатокБезРезерва - СтрокаТаблицыНоменклатуры.Количество;
			Иначе
				СтрокаТаблицыНоменклатуры.Остаток = 0;
			КонецЕсли;
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры
