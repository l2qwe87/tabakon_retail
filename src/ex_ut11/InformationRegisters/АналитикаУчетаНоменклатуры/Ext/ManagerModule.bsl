
&Вместо("ПолучитьНаборЗаписей")
Функция TBK_ПолучитьНаборЗаписей(ПараметрыАналитики)
	// В параметрах аналитики могут быть не все свойства
	СтруктураАналитики = Новый Структура("Номенклатура, Характеристика, Серия, МестоХранения, Назначение, СтатьяКалькуляции");
	
	ЗаполнитьЗначенияСвойств(СтруктураАналитики, ПараметрыАналитики);
	
	Если СтруктураАналитики.МестоХранения = Неопределено 
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ПараметрыАналитики, "Склад") Тогда
		
		// Вызывающий код может передавать место хранение в поле ""Склад""
		СтруктураАналитики.МестоХранения = ПараметрыАналитики.Склад;
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СтруктураАналитики.Номенклатура) Тогда
		
		ВызватьИсключение НСтр("ru = 'Ошибка при получении набора регистра сведений АналитикаУчетаНоменклатуры: не указана номенклатура.'");
		
	Иначе
		
		РеквизитыНоменклатуры = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СтруктураАналитики.Номенклатура, "ТипНоменклатуры,ИспользованиеХарактеристик");
		
		Если РеквизитыНоменклатуры.ТипНоменклатуры = Перечисления.ТипыНоменклатуры.МногооборотнаяТара Тогда 
			
			Если ЗначениеЗаполнено(СтруктураАналитики.Назначение)
				И Не ТипЗнч(СтруктураАналитики.МестоХранения) = Тип("СправочникСсылка.СтруктураПредприятия") Тогда
				ВызватьИсключение НСтр("ru = 'Ошибка при получении набора регистра сведений АналитикаУчетаНоменклатуры: указана номенклатура с типом ""Многооборотная тара"", и указано назначение.'")
			КонецЕсли;
			
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(РеквизитыНоменклатуры.ИспользованиеХарактеристик)
			Или РеквизитыНоменклатуры.ИспользованиеХарактеристик = Перечисления.ВариантыИспользованияХарактеристикНоменклатуры.НеИспользовать Тогда
			
			Если ЗначениеЗаполнено(СтруктураАналитики.Характеристика) Тогда
				ВызватьИсключение НСтр("ru = 'Ошибка при получении набора регистра сведений АналитикаУчетаНоменклатуры: указана для номенклатуры не настроено ведение характеристики, но характеристика указана.'")
			КонецЕсли;
			
		Иначе
			
			Если Не ЗначениеЗаполнено(СтруктураАналитики.Характеристика) Тогда
				//ВызватьИсключение НСтр("ru = 'Ошибка при получении набора регистра сведений АналитикаУчетаНоменклатуры: указана для номенклатуры настроено ведение характеристики, но характеристика не указана.'")
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураАналитики.Серия) Тогда
		
		Если ТипЗнч(СтруктураАналитики.МестоХранения) <> Тип("СправочникСсылка.Склады")
			И ТипЗнч(СтруктураАналитики.МестоХранения) <> Тип("СправочникСсылка.ДоговорыКонтрагентов")
			И ТипЗнч(СтруктураАналитики.МестоХранения) <> Тип("СправочникСсылка.СтруктураПредприятия") Тогда
			ВызватьИсключение НСтр("ru = 'Ошибка при получении набора регистра сведений АналитикаУчетаНоменклатуры: серия указана, но место хранения не того типа.'");
		КонецЕсли;
		
		Если РеквизитыНоменклатуры.ТипНоменклатуры <> Перечисления.ТипыНоменклатуры.Товар Тогда
			
			ВызватьИсключение НСтр("ru = 'Ошибка при получении набора регистра сведений АналитикаУчетаНоменклатуры: серия указана, но номенклатура не с типом ""Товар"".'");
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтруктураАналитики.СтатьяКалькуляции) Тогда
		Если ТипЗнч(СтруктураАналитики.МестоХранения) <> Тип("СправочникСсылка.СтруктураПредприятия") Тогда
			ВызватьИсключение НСтр("ru = 'Ошибка при получении набора регистра сведений АналитикаУчетаНоменклатуры: СтатьяКалькуляции указана, но место хранения не того типа.'");
		КонецЕсли;
	КонецЕсли;
	
	НаборЗаписей = РегистрыСведений.АналитикаУчетаНоменклатуры.СоздатьНаборЗаписей();
	
	Для Каждого КлючЗначение Из СтруктураАналитики Цикл
		НаборЗаписей.Отбор[КлючЗначение.Ключ].Установить(КлючЗначение.Значение);
	КонецЦикла;
	
	НоваяСтрока = НаборЗаписей.Добавить();
	
	ЗаполнитьЗначенияСвойств(НоваяСтрока, СтруктураАналитики);
	
	Возврат НаборЗаписей;

КонецФункции
