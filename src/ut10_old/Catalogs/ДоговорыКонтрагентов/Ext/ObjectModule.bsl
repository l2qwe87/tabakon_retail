////////////////////////////////////////////////////////////////////////////////
// ЭКСПОРТИРУЕМЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Функция проверяет, существуют ли ссылки на договор в движениях регистров накопления.
// Если есть - нельзя менять:
//  - Валюту взаиморасчетов
//  - Ведение взаиморасчетов.
//
// Параметры:
//  Нет.
//
// Возвращаемое значение:
//  Истина - если есть движения, Ложь - если нет.
//
Функция СуществуютСсылки() Экспорт

	Возврат ПолныеПрава.ПроверитьНаличиеСсылокНаДоговорКонтрагента(Ссылка);

КонецФункции //  СуществуютСсылки()

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// Процедура вызывается перед записью элемента справочника.
//
Процедура ПередЗаписью(Отказ)

	// Проверим можно ли изменять реквизиты договора.
	// Проверка осуществляется только если записывается уже существующий договор
	Если НЕ ОбменДанными.Загрузка И НЕ ЭтоНовый() Тогда

		Если ЭтоГруппа Тогда

			// Для группы владельца менять нельзя
			Если Владелец <> Ссылка.Владелец Тогда

				Сообщить("Нельзя изменять контрагента для группы договоров.", СтатусСообщения.Важное);
				Отказ = Истина;

			КонецЕсли; 

		Иначе

			// Проверим возможность смены владельца для договора
			Если Владелец <> Ссылка.Владелец Тогда

				Запрос = Новый Запрос;
				Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
				|	ДокументыПоДоговоруКонтрагента.Ссылка
				|ИЗ
				|	КритерийОтбора.ДокументыПоДоговоруКонтрагента(&Договор) КАК ДокументыПоДоговоруКонтрагента";
				
				Запрос.УстановитьПараметр("Договор", Ссылка);
				
				Результат = Запрос.Выполнить();
				ЕстьДокументыПоДоговору = НЕ Результат.Пустой();
				
				Если ЕстьДокументыПоДоговору Тогда
					Сообщить("Существуют документы, оформленные по договору """ + Наименование + """.
							 |Контрагент договора не может быть изменен, элемент не записан.", 
							 СтатусСообщения.Важное);
					Отказ = Истина;
				КонецЕсли; 

			КонецЕсли; 

			// Проверим возможность смены способа ведения взаиморасчетов и валюты взаиморасчетов
			Если ВедениеВзаиморасчетов <> Ссылка.ВедениеВзаиморасчетов
			 ИЛИ ВалютаВзаиморасчетов <> Ссылка.ВалютаВзаиморасчетов 
			 ИЛИ ВидДоговора <> Ссылка.ВидДоговора
			 ИЛИ Организация <> Ссылка.Организация
			 ИЛИ РасчетыВУсловныхЕдиницах <> Ссылка.РасчетыВУсловныхЕдиницах
			 ИЛИ ВидУсловийДоговора <> Ссылка.ВидУсловийДоговора 
			 ИЛИ ВестиПоДокументамРасчетовСКонтрагентом <> Ссылка.ВестиПоДокументамРасчетовСКонтрагентом Тогда

				Если ЭтотОбъект.СуществуютСсылки() Тогда

					Сообщить("Существуют документы, проведенные по договору """ + Наименование + """.
							 |Реквизиты ""Организация"", ""Ведение взаиморасчетов"", ""Валюта взаиморасчетов"", ""Вид договора"", 
							 |""Вести по документам расчетов с контрагентом"", ""Расчеты в условных единицах"" и ""Условия выполнения договора"" не могут быть изменены, элемент не записан.", 
							 СтатусСообщения.Важное);
					Отказ = Истина;

				КонецЕсли;

			КонецЕсли;
            Если  ОбособленныйУчетТоваровПоЗаказамПокупателей<>Ссылка.ОбособленныйУчетТоваровПоЗаказамПокупателей Тогда
				Если ПолныеПрава.ПроверитьНаличиеСсылокНаДоговорКонтрагентаВЗаказахПокупателей(Ссылка) Тогда
					Сообщить("Существуют заказы покупателей, проведенные по договору """ + Наименование + """.
					|Реквизит ""Обособленный учет товаров по заказам покупателей"" не может быть изменен, элемент не записан.", 
					СтатусСообщения.Важное);
					Отказ = Истина;

				КонецЕсли;
				
			КонецЕсли;
			
			// Если изменился порядок регистрации счетов-фактур на аванс, нужно сообщить
			Если ПорядокРегистрацииСчетовФактурНаАвансПоДоговору <> Ссылка.ПорядокРегистрацииСчетовФактурНаАвансПоДоговору Тогда
				Если ЭтотОбъект.СуществуютСсылки() Тогда
					Сообщить("Изменился порядок регистрации счетов-фактур на аванс по договору. 
							|При регистрации счетов-фактур на аванс за прошлые периоды возможны изменения в ранее зарегистрированных документах.", СтатусСообщения.Важное);
				КонецЕсли;
			КонецЕсли;

		КонецЕсли;

	КонецЕсли;

	// Проверим заполнение и очистим неиспользуемые реквизиты элемента договора.
	Если Не ЭтоГруппа Тогда

		// Проверим, заполнена ли валюта.
		Если НЕ ОбменДанными.Загрузка И НЕ ЗначениеЗаполнено(ВалютаВзаиморасчетов) Тогда
			Сообщить("Не указана валюта договора.", СтатусСообщения.Важное);
			Отказ = Истина;
		КонецЕсли;

		// Проверим, заполнена ли организация.
		Если НЕ ОбменДанными.Загрузка И НЕ ЗначениеЗаполнено(Организация) Тогда
			Сообщить("Не указана организация, от которой заключен договор.", СтатусСообщения.Важное);
			Отказ = Истина;
		КонецЕсли;

		// Проверим, заполнен ли способ ведения взаиморасчетов.
		Если НЕ ОбменДанными.Загрузка И НЕ ЗначениеЗаполнено(ВедениеВзаиморасчетов) Тогда
			Сообщить("Не указан способ ведения взаиморасчетов по договору.", СтатусСообщения.Важное);
			Отказ = Истина;
		КонецЕсли;

		// Проверим, заполнен ли вид договора.
		Если НЕ ОбменДанными.Загрузка Тогда
		
			Если НЕ ЗначениеЗаполнено(ВидДоговора) Тогда
				Сообщить("Не указан вид договора.", СтатусСообщения.Важное);
				Отказ = Истина;
			Иначе
				// Проверим, правильно ли заполнен вид договора
				Если (ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПокупателем ИЛИ ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионером)
					И НЕ Владелец.Покупатель Тогда
					Сообщить("Вид договора ""С покупателем"" может устанавливаться только когда у контрагента указано что он является покупателем.", СтатусСообщения.Важное);
					Отказ = Истина;
				ИначеЕсли (ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком ИЛИ ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом)
					И НЕ Владелец.Поставщик Тогда
					Сообщить("Вид договора ""С поставщиком"" может устанавливаться только когда у контрагента указано что он является поставщиком.", СтатусСообщения.Важное);
					Отказ = Истина;
				ИначеЕсли ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.Прочее
					И ВестиПоДокументамРасчетовСКонтрагентом Тогда
					Сообщить("Флажок ""Вести по документам расчетов с контрагентами"" не может устанавливаться у договоров с видом ""Прочее"".", СтатусСообщения.Важное);
					Отказ = Истина;
				КонецЕсли; 
			КонецЕсли;
		
		КонецЕсли; 

//<pozdniakov.a
		// Проверим, заполнены ли дата окончания срока действия и ответственный при указанном количестве дней оповещения
		Если НЕ ОбменДанными.Загрузка Тогда
			Если НЕ ЭтотОбъект.Закрыт И НЕ ЗначениеЗаполнено(ДопСоглашение) Тогда
				Если КоличествоДнейОповещения <> 0 Тогда
					Если НЕ ЗначениеЗаполнено(СрокДействия) Тогда
						Сообщить("Не заполнен срок действия договора при указанном количестве дней оповещения о нем!", СтатусСообщения.Важное);
						Отказ = Истина;
					КонецЕсли;
					
					Если НЕ ЗначениеЗаполнено(Ответственный) Тогда
						Сообщить("Не заполнен ответственный за ведение договора!", СтатусСообщения.Важное);
						Отказ = Истина;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
//pozdniakov.a>
	КонецЕсли;

КонецПроцедуры // ПередЗаписью()



//<pozdniakov.a
Функция СоздатьИзменитьНапоминаниеПользователю() Экспорт
	// Если договор закрыт или по нему имеется доп соглашение - не создаем напоминание, т.к. не требуется
	Если Закрыт ИЛИ ЗначениеЗаполнено(ДопСоглашение) Тогда Возврат Истина; КонецЕсли;
	
	// Если по договору не указано количество дней напоминания - не создаем напоминание, т.к. не требуется
	Если КоличествоДнейОповещения = 0 Тогда Возврат Истина; КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СрокДействия) Тогда
		#Если Клиент Тогда
			Сообщить("Не заполнен срок действия договора при указанном количестве дней оповещения о нем!", СтатусСообщения.Важное);
		#КонецЕсли
		
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Ответственный) Тогда
		#Если Клиент Тогда
			Сообщить("Не заполнен ответственный за ведение договора!", СтатусСообщения.Важное);
		#КонецЕсли
		
		Возврат Ложь;	
	КонецЕсли;
	
	ТекПользователь = глЗначениеПеременной("глТекущийПользователь");
	
	ЗадачаНапоминание = Неопределено;
	стрНачалоОписания = "Истекает срок действия договора";
	
	// Уже созданную (возможно) задачу ищем только по объекту и начальной строке описания, 	
	// т.к. ответственный за ведение договора (исполнитель задачи) всегда может быть изменен
	// пользователями вручную (в форме договора)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ЗадачиПользователя.Ссылка КАК Задача
		|ИЗ
		|	Задача.ЗадачиПользователя КАК ЗадачиПользователя
		|ГДЕ
		|	ЗадачиПользователя.Объект = &Объект
		|	И НЕ ЗадачиПользователя.Выполнена
		|	И НЕ ЗадачиПользователя.ПометкаУдаления
		|	И ЗадачиПользователя.Описание ПОДОБНО """ + стрНачалоОписания + "%""
		|
		|УПОРЯДОЧИТЬ ПО
		|	ЗадачиПользователя.Дата УБЫВ";
	
	Запрос.УстановитьПараметр("Объект", ЭтотОбъект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() ТОгда
		ЗадачаНапоминание = Задачи.ЗадачиПользователя.СоздатьЗадачу();
		ЗадачаНапоминание.Дата = ТекущаяДата();
		ЗадачаНапоминание.Инициатор = ТекПользователь;
	Иначе
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		ВыборкаДетальныеЗаписи.Следующий();
		ЗадачаНапоминание = ВыборкаДетальныеЗаписи.Задача.ПолучитьОбъект();
	КонецЕсли;
	
	ЗадачаНапоминание.Наименование 	 = "Напоминание по объекту: " + ЭтотОбъект.Метаданные().Синоним;
	ЗадачаНапоминание.Объект       	 = ЭтотОбъект.Ссылка;
	ЗадачаНапоминание.Исполнитель  	 = Ответственный;
	ЗадачаНапоминание.Оповещение   	 = Истина;
	ЗадачаНапоминание.СрокИсполнения = СрокДействия;
	ЗадачаНапоминание.СрокОповещения = СрокДействия - КоличествоДнейОповещения * 86400;
	ЗадачаНапоминание.Описание		 = стрНачалоОписания + ": " + ЭтотОбъект.Наименование + ", по контрагенту: " + Строка(ЭтотОбъект.Владелец) 
										+ Символы.ПС + "Необходимо закрыть данный договор, либо внести по нему доп. соглашение." + Символы.ПС + "(см. Основание)";
	
	Попытка
		ЗадачаНапоминание.Записать();
	Исключение
		#Если Клиент Тогда
			Сообщить("Ошибка записи задачи по договору: " + ОписаниеОшибки(), СтатусСообщения.Важное);
		#КонецЕсли
		
		Возврат Ложь;	
	КонецПопытки;
	
	Возврат Истина;
КонецФункции
//pozdniakov.a>

