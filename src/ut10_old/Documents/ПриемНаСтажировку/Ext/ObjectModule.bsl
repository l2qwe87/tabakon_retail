// Заполняет реквизиты значениями по умолчанию
//
// Параметры: 
//  ПараметрОбъектКопирования	- содержкит ссылку на документ копирования в случае, 
//								  если новый документ создается копированием.
//  ПараметрОснование			- содержкит ссылку на документ-основание в случае, 
//								  если новый документ создается на основании другого.
//
Процедура ИнициализироватьНовыйДокумент(ПараметрОбъектКопирования, ПараметрОснование) Экспорт

	#Если Клиент Или ВнешнееСоединение Тогда
	ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект, глЗначениеПеременной("глТекущийПользователь"), , , ПараметрОбъектКопирования, ПараметрОснование);
	#КонецЕсли

КонецПроцедуры // ИнициализироватьНовыйДокумент()	

Процедура ОбработкаПроведения(Отказ, Режим)
	// Проверяем наличие кадровых событий по сотруднику в указанную дату документа (созданных другими документами)
	// Если таковые имеются, отменяем проведение документа с сообщением пользователю об ошибке.
	Если РегистрыСведений.КадровыйУчетСотрудников.НаличиеКадровыхСобытийНаДату(Ссылка, Дата, Новый Структура("Организация,Сотрудник", Организация, Стажер)) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	// регистр КадровыйУчетСотрудников
	Движения.КадровыйУчетСотрудников.Записывать = Истина;
	Движения.КадровыйУчетСотрудников.Очистить();
	Движение = Движения.КадровыйУчетСотрудников.Добавить();
	Движение.Период = НачалоДня(Дата);
	Движение.Организация = Организация;	
	Движение.Сотрудник = Стажер;
	Движение.Состояние = Перечисления.СостоянияСотрудников.ПринятНаСтажировку;
	Движение.Должность = Должность;
	Движение.Подразделение = Подразделение;

	// регистр СтавкиОсновныхВидовРасчетовСотрудников
	Движения.СтавкиОсновныхВидовРасчетовСотрудников.Записывать = Истина;
	Движения.СтавкиОсновныхВидовРасчетовСотрудников.Очистить();
	Движение = Движения.СтавкиОсновныхВидовРасчетовСотрудников.Добавить();
	Движение.Период = Дата;
	Движение.Сотрудник = Стажер;
	Движение.ВидРасчета = Справочники.ВидыРасчетовССотрудниками.ОкладСменыСтажера;
	Движение.Сумма = СтавкаЗаСмену;
	
	// Перемещение сотрудника в группу стажеров
	СпрОбъект = Стажер.ПолучитьОбъект();
	СпрОбъект.Родитель = Справочники.ФизическиеЛица.Стажеры;
	СпрОбъект.Записать();
КонецПроцедуры
