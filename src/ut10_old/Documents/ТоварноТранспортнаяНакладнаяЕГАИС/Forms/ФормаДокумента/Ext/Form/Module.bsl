
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.СтатусОбработкиТТН_ЕГАИС = Перечисления.СтатусыОбработкиТТН_ЕГАИС.ПереданОтветВЕГАИС 
		ИЛИ Объект.СтатусОбработкиТТН_ЕГАИС = Перечисления.СтатусыОбработкиТТН_ЕГАИС.ПереданОтказВЕГАИС Тогда
		Элементы.ФормаОтказатьсяОтТТН.Доступность = Ложь;
	КонецЕсли;
	
	УстановитьКартинкуСтатуса();
	
	ПроверитьИнформациюОбОшибке();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОтказатьсяОтТТН(Команда)
	
	Если Объект.СтатусОбработкиТТН_ЕГАИС = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиТТН_ЕГАИС.ПереданОтветВЕГАИС") Тогда
		СтрокаСообщения = НСтр("ru = 'Накладная уже подтверждена актом, отказ не возможен'");
		Элементы.ФормаОтказатьсяОтТТН.Доступность = Ложь;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения);
		Возврат;
	ИначеЕсли Объект.СтатусОбработкиТТН_ЕГАИС = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиТТН_ЕГАИС.ПереданОтказВЕГАИС") Тогда
		СтрокаСообщения = НСтр("ru = 'Отказ от данных уже передавался в ЕГАИС'");
		Элементы.ФормаОтказатьсяОтТТН.Доступность = Ложь;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения);
		Возврат;
	ИначеЕсли Объект.СтатусОбработкиТТН_ЕГАИС = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиТТН_ЕГАИС.Обрабатывается") Тогда
		ТекстВопроса = НСтр("ru='Накладная уже обрабатывается. Вы уверены, что хотите передать отказ от накладной в ЕГАИС?'");
	Иначе
		ТекстВопроса = НСтр("ru='Вы уверены, что хотите передать отказ от накладной в ЕГАИС?'");
	КонецЕсли;
	
	Если Вопрос(ТекстВопроса, РежимДиалогаВопрос.ДаНет) <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыАкта = Новый Структура;
	ПараметрыАкта.Вставить("ТТН", Объект.Ссылка);
	ПараметрыАкта.Вставить("Отказ", Истина);
	
	РезультатОперации = ИнтеграцияЕГАИСКлиент.ВыгрузитьАктПодтвержденияТТН(ПараметрыАкта);
	
	Если НЕ РезультатОперации.Результат Тогда
		СтрокаСообщения = НСтр("ru = 'Ошибка передачи данных в ЕГАИС'")
			+ Символы.ПС + РезультатОперации.ОписаниеОшибки;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрокаСообщения);
	Иначе
		Объект.СтатусОбработкиТТН_ЕГАИС = ПредопределенноеЗначение("Перечисление.СтатусыОбработкиТТН_ЕГАИС.ПереданОтказВЕГАИС");
		Объект.ЕстьОшибкиПередачиЗапроса = Ложь;
		Объект.ИдентификаторПоследнегоЗапроса = РезультатОперации.ИдентификаторЗапроса;
		
		РезультатЗаписи = Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Запись));
		
		Если РезультатЗаписи Тогда
			Оповестить("ОбновитьСписокТТН_ЕГАИС");
			Элементы.ФормаОтказатьсяОтТТН.Доступность = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура УстановитьКартинкуСтатуса()
	
	ПустойСтатус = Ложь;
	
	Если Объект.СтатусОбработкиТТН_ЕГАИС = Перечисления.СтатусыОбработкиТТН_ЕГАИС.ПустаяСсылка() Тогда
		КартинкаСостоянияДокумента = 0;
		ПустойСтатус = Истина;
	ИначеЕсли Объект.СтатусОбработкиТТН_ЕГАИС = Перечисления.СтатусыОбработкиТТН_ЕГАИС.Обрабатывается Тогда
		КартинкаСостоянияДокумента = 1;
	ИначеЕсли Объект.СтатусОбработкиТТН_ЕГАИС = Перечисления.СтатусыОбработкиТТН_ЕГАИС.ПереданОтветВЕГАИС Тогда
		КартинкаСостоянияДокумента = 2;
	Иначе
		КартинкаСостоянияДокумента = 4;
	КонецЕсли;
	
	Если ПустойСтатус Тогда
		Элементы.ГруппаСтраницыСтатусы.ТекущаяСтраница = Элементы.ГруппаСтраницаПустойСтатус;
	Иначе
		Элементы.ГруппаСтраницыСтатусы.ТекущаяСтраница = Элементы.ГруппаСтраницаСтатус;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьИнформациюОбОшибке()

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗапросыЕГАИС.Комментарий
	|ИЗ
	|	РегистрСведений.ЗапросыЕГАИС КАК ЗапросыЕГАИС
	|ГДЕ
	|	ЗапросыЕГАИС.Идентификатор = &Идентификатор
	|	И НЕ ЗапросыЕГАИС.Комментарий ПОДОБНО """"";
	
	Запрос.УстановитьПараметр("Идентификатор", Объект.ИдентификаторПоследнегоЗапроса);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
	
		Элементы.ГруппаИнформацияОбОшибке.Видимость = Истина;
		ИнформацияОбОшибке = Выборка.Комментарий;
	
	КонецЕсли;

КонецПроцедуры