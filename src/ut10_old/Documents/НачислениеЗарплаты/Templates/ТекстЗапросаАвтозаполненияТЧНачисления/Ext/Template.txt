// Таблица документов с данными о рабочих сменах
ВЫБРАТЬ
	ОтчетОРозничныхПродажах.Дата,
	ОтчетОРозничныхПродажахПродавцы.Ссылка,
	ОтчетОРозничныхПродажахПродавцы.Продавец,
	ОтчетОРозничныхПродажахПродавцы.ЭтоГлавныйПродавец,
	ОтчетОРозничныхПродажахПродавцы.ЭтоДопСмена
	
	ПОМЕСТИТЬ ВТ_ДокументыРабочихСмен
ИЗ
	Документ.ОтчетОРозничныхПродажах.Продавцы КАК ОтчетОРозничныхПродажахПродавцы
		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
		ПО ОтчетОРозничныхПродажахПродавцы.Ссылка = ОтчетОРозничныхПродажах.Ссылка
ГДЕ
	ОтчетОРозничныхПродажах.Проведен
	И ОтчетОРозничныхПродажах.Организация = &Организация
	И ОтчетОРозничныхПродажах.Дата МЕЖДУ НАЧАЛОПЕРИОДА(&ДатаНачала, МЕСЯЦ) И КОНЕЦПЕРИОДА(&ДатаОкончания, МЕСЯЦ)

;
// Таблица - список сотрудников (из рабочих смен)
ВЫБРАТЬ РАЗЛИЧНЫЕ
	ВТ_ДокументыРабочихСмен.Продавец

	ПОМЕСТИТЬ ВТ_Сотрудники
ИЗ
	ВТ_ДокументыРабочихСмен	
	
;

// Получение количества рабочих смен сотрудников:
// 1) В пределах расчетного периода
ВЫБРАТЬ
	Сотрудники.Продавец,
	КОЛИЧЕСТВО(РабочиеСмены.Ссылка) КАК КоличествоСменПериода
	
	ПОМЕСТИТЬ ВТ_КоличествоСменСотрудниковЗаПериодРасчета
ИЗ 
	ВТ_Сотрудники КАК Сотрудники
		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДокументыРабочихСмен КАК РабочиеСмены
		ПО Сотрудники.Продавец = РабочиеСмены.Продавец

СГРУППИРОВАТЬ ПО
	Сотрудники.Продавец 
	
;                

// 2) С начала работы (организацию в расчет не берем)
ВЫБРАТЬ
	Сотрудники.Продавец,
	КОЛИЧЕСТВО(ОтчетОРозничныхПродажахПродавцы.Ссылка) КАК КоличествоСменОбщее
	
	ПОМЕСТИТЬ ВТ_КоличествоСменСотрудниковОбщее
ИЗ 
	ВТ_Сотрудники КАК Сотрудники
		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах.Продавцы КАК ОтчетОРозничныхПродажахПродавцы
		ПО Сотрудники.Продавец = ОтчетОРозничныхПродажахПродавцы.Продавец

СГРУППИРОВАТЬ ПО
	Сотрудники.Продавец 
	
;                

// 3) Общая таблица данных рабочих смен
ВЫБРАТЬ
	ВТ_КоличествоСменСотрудниковЗаПериодРасчета.Продавец,
	ВТ_КоличествоСменСотрудниковЗаПериодРасчета.КоличествоСменПериода,
	ВТ_КоличествоСменСотрудниковОбщее.КоличествоСменОбщее
	
	ПОМЕСТИТЬ ВТ_ОбщиеДанныеКоличестваСмен
ИЗ
	ВТ_КоличествоСменСотрудниковЗаПериодРасчета
	ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	ВТ_КоличествоСменСотрудниковОбщее
	ПО ВТ_КоличествоСменСотрудниковЗаПериодРасчета.Продавец = ВТ_КоличествоСменСотрудниковОбщее.Продавец

;

УНИЧТОЖИТЬ ВТ_КоличествоСменСотрудниковЗаПериодРасчета;
УНИЧТОЖИТЬ ВТ_КоличествоСменСотрудниковОбщее;

//*********************************************************************
// Даты кадровых состояний всех сотрудников из документов рабочих смен

ВЫБРАТЬ
	КадровыйУчетСотрудников.Период,
	КадровыйУчетСотрудников.Состояние,
	КадровыйУчетСотрудников.Сотрудник

	ПОМЕСТИТЬ ВТ_ДатыКадровыхСостоянийСотрудников
ИЗ
	РегистрСведений.КадровыйУчетСотрудников КАК КадровыйУчетСотрудников
ГДЕ
	КадровыйУчетСотрудников.Сотрудник В(ВЫБРАТЬ ВТ_Сотрудники.Продавец ИЗ ВТ_Сотрудники)
	И КадровыйУчетСотрудников.Организация = &Организация
	И КадровыйУчетСотрудников.Период МЕЖДУ НАЧАЛОПЕРИОДА(&ДатаНачала, МЕСЯЦ) И КОНЕЦПЕРИОДА(&ДатаОкончания, МЕСЯЦ)
	
;

// Получение соответствий дат рабочих смен сотрудников последним актуальным датам их кадровых состояний
ВЫБРАТЬ
	ДокументыРабочихСмен.Дата,
	ДокументыРабочихСмен.Продавец,
	МАКСИМУМ(ДатыКадровыхСостоянийСотрудников.Период) КАК ДатаСостояния
	
	ПОМЕСТИТЬ ВТ_ДатыСостоянийНаДатыСмен	
ИЗ
	ВТ_ДокументыРабочихСмен КАК ДокументыРабочихСмен
		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_ДатыКадровыхСостоянийСотрудников КАК ДатыКадровыхСостоянийСотрудников
			ПО ДокументыРабочихСмен.Продавец = ДатыКадровыхСостоянийСотрудников.Сотрудник
ГДЕ
	ДокументыРабочихСмен.Дата >= ДатыКадровыхСостоянийСотрудников.Период

СГРУППИРОВАТЬ ПО
	ДокументыРабочихСмен.Дата,
	ДокументыРабочихСмен.Продавец

;

// Получение кадровых состояний сотрудников на даты их рабочих смен
ВЫБРАТЬ
	ДатыСостоянийНаДатыСмен.Дата,
	ДатыСостоянийНаДатыСмен.Продавец,
	ДатыКадровыхСостоянийСотрудников.Состояние
	
	ПОМЕСТИТЬ ВТ_КадровыеСостоянияНаДатыСмен
ИЗ
	ВТ_ДатыСостоянийНаДатыСмен КАК ДатыСостоянийНаДатыСмен
	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДатыКадровыхСостоянийСотрудников КАК ДатыКадровыхСостоянийСотрудников
		ПО ДатыСостоянийНаДатыСмен.ДатаСостояния = ДатыКадровыхСостоянийСотрудников.Период

;
УНИЧТОЖИТЬ ВТ_ДатыКадровыхСостоянийСотрудников;	
УНИЧТОЖИТЬ ВТ_ДатыСостоянийНаДатыСмен;	

//*********************************************************************
// Ставки видов расчетов всех сотрудников из документов рабочих смен
ВЫБРАТЬ
	СтавкиОсновныхВидовРасчетовСотрудников.Период,
	СтавкиОсновныхВидовРасчетовСотрудников.Сотрудник,
	СтавкиОсновныхВидовРасчетовСотрудников.ВидРасчета,
	СтавкиОсновныхВидовРасчетовСотрудников.Сумма
	
	ПОМЕСТИТЬ ВТ_СтавкиСотрудниковПоВидамРасчетов
ИЗ
	РегистрСведений.СтавкиОсновныхВидовРасчетовСотрудников КАК СтавкиОсновныхВидовРасчетовСотрудников
ГДЕ
	СтавкиОсновныхВидовРасчетовСотрудников.Сотрудник В(ВЫБРАТЬ ВТ_Сотрудники.Продавец ИЗ ВТ_Сотрудники)	
	И СтавкиОсновныхВидовРасчетовСотрудников.ВидРасчета В (&СписокОсновныхВР)

;

// Получение соответствий дат рабочих смен сотрудников последним актуальным датам начала действия ставок основных видов расчетов
ВЫБРАТЬ
	ДокументыРабочихСмен.Дата,
	ДокументыРабочихСмен.Продавец,
	ДокументыРабочихСмен.ЭтоДопСмена,
	СтавкиСотрудниковПоВидамРасчетов.ВидРасчета,
	МАКСИМУМ(СтавкиСотрудниковПоВидамРасчетов.Период) КАК МаксПериод
	
	ПОМЕСТИТЬ ВТ_ВидыРасчетовПоДатамСмен
ИЗ
	ВТ_ДокументыРабочихСмен КАК ДокументыРабочихСмен
		ПОЛНОЕ СОЕДИНЕНИЕ ВТ_СтавкиСотрудниковПоВидамРасчетов КАК СтавкиСотрудниковПоВидамРасчетов
			ПО ДокументыРабочихСмен.Продавец = СтавкиСотрудниковПоВидамРасчетов.Сотрудник
ГДЕ
	ДокументыРабочихСмен.Дата >= СтавкиСотрудниковПоВидамРасчетов.Период

СГРУППИРОВАТЬ ПО
	ДокументыРабочихСмен.Дата,
	ДокументыРабочихСмен.Продавец,
	ДокументыРабочихСмен.ЭтоДопСмена,
	СтавкиСотрудниковПоВидамРасчетов.ВидРасчета
	
;

// Получение сумм по каждому основному виду расчетов сотрудников и состояний на каждую дату отработанной смены
ВЫБРАТЬ
	ВидыРасчетовПоДатамСмен.*,
	КадровыеСостоянияНаДатыСмен.Состояние,
	ВидыРасчетовССотрудниками.КоличествоСменДней КАК КоличествоСменДнейРасчета,
	ОбщиеДанныеКоличестваСмен.КоличествоСменПериода,
	ОбщиеДанныеКоличестваСмен.КоличествоСменОбщее,
	СтавкиСотрудниковПоВидамРасчетов.Сумма
	
	ПОМЕСТИТЬ ВТ_СуммыПоВидамРасчетовСотрудников
ИЗ
	ВТ_ВидыРасчетовПоДатамСмен КАК ВидыРасчетовПоДатамСмен
		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтавкиСотрудниковПоВидамРасчетов КАК СтавкиСотрудниковПоВидамРасчетов
			ПО ВидыРасчетовПоДатамСмен.Продавец = СтавкиСотрудниковПоВидамРасчетов.Сотрудник
			И ВидыРасчетовПоДатамСмен.ВидРасчета = СтавкиСотрудниковПоВидамРасчетов.ВидРасчета 
			И ВидыРасчетовПоДатамСмен.МаксПериод = СтавкиСотрудниковПоВидамРасчетов.Период
		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыРасчетовССотрудниками КАК ВидыРасчетовССотрудниками
			ПО ВидыРасчетовПоДатамСмен.ВидРасчета = ВидыРасчетовССотрудниками.Ссылка
		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ОбщиеДанныеКоличестваСмен КАК ОбщиеДанныеКоличестваСмен
			ПО ВидыРасчетовПоДатамСмен.Продавец = ОбщиеДанныеКоличестваСмен.Продавец
		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_КадровыеСостоянияНаДатыСмен КАК КадровыеСостоянияНаДатыСмен
			ПО ВидыРасчетовПоДатамСмен.Продавец = КадровыеСостоянияНаДатыСмен.Продавец
				И ВидыРасчетовПоДатамСмен.Дата = КадровыеСостоянияНаДатыСмен.Дата
ГДЕ
	СтавкиСотрудниковПоВидамРасчетов.Сумма <> 0	

;

//Расчет сумм основных начислений (и удержаний)
ВЫБРАТЬ
	СуммыПоВидамРасчетовСотрудников.Продавец,
	ВЫБОР КОГДА
			НЕ СуммыПоВидамРасчетовСотрудников.ЭтоДопСмена И СуммыПоВидамРасчетовСотрудников.ВидРасчета = ЗНАЧЕНИЕ(Справочник.ВидыРасчетовССотрудниками.ОкладВДень) ТОГДА СУММА(СуммыПоВидамРасчетовСотрудников.Сумма) 		
		ИНАЧЕ 
			0
		КОНЕЦ КАК Оклад,
	ВЫБОР КОГДА 
			СуммыПоВидамРасчетовСотрудников.КоличествоСменОбщее >= СуммыПоВидамРасчетовСотрудников.КоличествоСменДнейРасчета И СуммыПоВидамРасчетовСотрудников.ВидРасчета = ЗНАЧЕНИЕ(Справочник.ВидыРасчетовССотрудниками.ОкладСменыСтажера) ТОГДА СУММА(СуммыПоВидамРасчетовСотрудников.Сумма) 
		ИНАЧЕ 
			0
		КОНЕЦ КАК ОкладСтажера,
	ВЫБОР КОГДА 
			СуммыПоВидамРасчетовСотрудников.ЭтоДопСмена И СуммыПоВидамРасчетовСотрудников.ВидРасчета = ЗНАЧЕНИЕ(Справочник.ВидыРасчетовССотрудниками.ДопВыход) ТОГДА СУММА(СуммыПоВидамРасчетовСотрудников.Сумма) 
		ИНАЧЕ
			0 
		КОНЕЦ КАК ДопСмена,
	ВЫБОР КОГДА 
			СуммыПоВидамРасчетовСотрудников.КоличествоСменПериода >= СуммыПоВидамРасчетовСотрудников.КоличествоСменДнейРасчета И СуммыПоВидамРасчетовСотрудников.ВидРасчета = ЗНАЧЕНИЕ(Справочник.ВидыРасчетовССотрудниками.ДопПремия) ТОГДА СУММА(СуммыПоВидамРасчетовСотрудников.Сумма) 
		ИНАЧЕ
			0 
		КОНЕЦ КАК ДопПремия,
	ВЫБОР КОГДА
			НЕ СуммыПоВидамРасчетовСотрудников.ЭтоДопСмена 
			И СуммыПоВидамРасчетовСотрудников.ВидРасчета = ЗНАЧЕНИЕ(Справочник.ВидыРасчетовССотрудниками.ОкладВДень)
			И СуммыПоВидамРасчетовСотрудников.Состояние = ЗНАЧЕНИЕ(Перечисление.СостоянияСотрудников.РаботаетОфициально) 
				ТОГДА 
					СУММА(СуммыПоВидамРасчетовСотрудников.Сумма/100*13) 		
		ИНАЧЕ 
			0
		КОНЕЦ КАК НДФЛ
	ПОМЕСТИТЬ ВТ_РезультатыОсновныхНачислений
	
ИЗ
	ВТ_СуммыПоВидамРасчетовСотрудников КАК СуммыПоВидамРасчетовСотрудников

СГРУППИРОВАТЬ ПО
	СуммыПоВидамРасчетовСотрудников.Продавец,
	СуммыПоВидамРасчетовСотрудников.ЭтоДопСмена,
	СуммыПоВидамРасчетовСотрудников.ВидРасчета,
	СуммыПоВидамРасчетовСотрудников.Состояние,
	СуммыПоВидамРасчетовСотрудников.КоличествоСменДнейРасчета,
	СуммыПоВидамРасчетовСотрудников.КоличествоСменПериода,
	СуммыПоВидамРасчетовСотрудников.КоличествоСменОбщее
	
;

// Итоги результатов расчетов по основным начислениям
ВЫБРАТЬ
	РезультатыОсновныхНачислений.Продавец,
	СУММА(РезультатыОсновныхНачислений.Оклад) КАК Оклад,
	СУММА(РезультатыОсновныхНачислений.ОкладСтажера) КАК ОкладСтажера,
	СУММА(РезультатыОсновныхНачислений.ДопСмена) КАК ДопСмена,
	СУММА(РезультатыОсновныхНачислений.ДопПремия) КАК ДопПремия,
	СУММА(РезультатыОсновныхНачислений.НДФЛ) КАК НДФЛ
	
	ПОМЕСТИТЬ ВТ_ИтогиРезультатовОсновныхНачислений
ИЗ
	ВТ_РезультатыОсновныхНачислений КАК РезультатыОсновныхНачислений
	
СГРУППИРОВАТЬ ПО
	РезультатыОсновныхНачислений.Продавец

;

УНИЧТОЖИТЬ ВТ_СтавкиСотрудниковПоВидамРасчетов;
УНИЧТОЖИТЬ ВТ_ОбщиеДанныеКоличестваСмен;
УНИЧТОЖИТЬ ВТ_КадровыеСостоянияНаДатыСмен;
УНИЧТОЖИТЬ ВТ_СуммыПоВидамРасчетовСотрудников;
УНИЧТОЖИТЬ ВТ_РезультатыОсновныхНачислений;

//****************************************************************************************************************
//Расчет сумм дополнительных начислений и удержаний
ВЫБРАТЬ
	РасчетыПоЗП.Сотрудник,
	ВЫБОР КОГДА
		РасчетыПоЗП.ВидРасчета = ЗНАЧЕНИЕ(Справочник.ВидыРасчетовССотрудниками.Премия) ТОГДА СУММА(РасчетыПоЗП.Сумма)
	ИНАЧЕ
		0
	КОНЕЦ КАК Премия,
	ВЫБОР КОГДА
		СпрВидыРасчетов.ХарактерРасчета = ЗНАЧЕНИЕ(Перечисление.ХарактерРасчета.Удержание) 
		И СпрВидыРасчетов.Ссылка <> ЗНАЧЕНИЕ(Справочник.ВидыРасчетовССотрудниками.ПодоходныйНалог) 
		И СпрВидыРасчетов.Ссылка <> ЗНАЧЕНИЕ(Справочник.ВидыРасчетовССотрудниками.УдержаниеЗаВозвратАкционногоТовара) ТОГДА СУММА(РасчетыПоЗП.Сумма)
	ИНАЧЕ
		0
	КОНЕЦ КАК Удержания
	
	ПОМЕСТИТЬ ВТ_РасчетыДополнительныхНачисленийУдержаний
	
ИЗ
	РегистрНакопления.РасчетыПоЗаработнойПлате КАК РасчетыПоЗП
		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыРасчетовССотрудниками КАК СпрВидыРасчетов
			ПО РасчетыПоЗП.ВидРасчета = СпрВидыРасчетов.Ссылка
ГДЕ
	РасчетыПоЗП.Организация = &Организация
	И РасчетыПоЗП.Период МЕЖДУ НАЧАЛОПЕРИОДА(&ДатаНачала, МЕСЯЦ) И КОНЕЦПЕРИОДА(&ДатаОкончания, МЕСЯЦ)
	И РасчетыПоЗП.Сотрудник В (ВЫБРАТЬ ВТ_Сотрудники.Продавец ИЗ ВТ_Сотрудники)

СГРУППИРОВАТЬ ПО
	РасчетыПоЗП.Сотрудник,
	СпрВидыРасчетов.Ссылка,
	РасчетыПоЗП.ВидРасчета
	
;

// Итоги расчетов сумм дополнительных начислений и удержаний
ВЫБРАТЬ
	РасчетыПоЗП.Сотрудник,
	СУММА(РасчетыПоЗП.Премия) КАК Премия,
	СУММА(РасчетыПоЗП.Удержания) КАК Удержания
//	СУММА(РасчетыПоЗП.) КАК ,

	ПОМЕСТИТЬ ВТ_ИтогиДополнительныхНачисленийУдержаний
ИЗ
	ВТ_РасчетыДополнительныхНачисленийУдержаний КАК РасчетыПоЗП
	
СГРУППИРОВАТЬ ПО
	РасчетыПоЗП.Сотрудник
	
;
УНИЧТОЖИТЬ ВТ_РасчетыДополнительныхНачисленийУдержаний;

//****************************************************************************************************************
// Таблица детальных записей по премиям за акционные товары
ВЫБРАТЬ
	ОтчетОРозничныхПродажахПродавцы.Продавец,
	ВЫБОР 
		КОГДА УстановкаПремииЗаАкционныеТовары.ВариантРасчетаПремии = ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаСкидокНадбавок.Процентом)
			ТОГДА ВЫРАЗИТЬ(ОтчетОРозничныхПродажахТовары.Сумма*УстановкаПремииЗаАкционныеТовары.Величина/100 КАК ЧИСЛО(15, 2))
		КОГДА УстановкаПремииЗаАкционныеТовары.ВариантРасчетаПремии = ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаСкидокНадбавок.Суммой)
			ТОГДА УстановкаПремииЗаАкционныеТовары.Величина
		ИНАЧЕ 
			0
	КОНЕЦ КАК СуммаПремии
	
	ПОМЕСТИТЬ ВТ_ПремииЗаАкционныеТоварыДетальная
ИЗ
	Документ.ОтчетОРозничныхПродажах.Товары КАК ОтчетОРозничныхПродажахТовары
		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
			ПО ОтчетОРозничныхПродажахТовары.Ссылка = ОтчетОРозничныхПродажах.Ссылка
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УстановкаПремииЗаАкционныйТовар.Товары КАК УстановкаПремииЗаАкционныеТовары
	   		ПО ОтчетОРозничныхПродажахТовары.Номенклатура = УстановкаПремииЗаАкционныеТовары.Номенклатура
	   		И УстановкаПремииЗаАкционныеТовары.Ссылка.Проведен                  
	   		И ОтчетОРозничныхПродажах.Дата МЕЖДУ УстановкаПремииЗаАкционныеТовары.НачалоАкции И УстановкаПремииЗаАкционныеТовары.ОкончаниеАкции
		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах.Продавцы КАК ОтчетОРозничныхПродажахПродавцы
			ПО ОтчетОРозничныхПродажахТовары.Ссылка = ОтчетОРозничныхПродажахПродавцы.Ссылка
			И ОтчетОРозничныхПродажахПродавцы.ЭтоГлавныйПродавец
ГДЕ
	ОтчетОРозничныхПродажахТовары.Ссылка В (ВЫБРАТЬ РАЗЛИЧНЫЕ ВТ_ДокументыРабочихСмен.Ссылка ИЗ ВТ_ДокументыРабочихСмен)
	
;

// Итоговая таблица данных по премиям за акционные товары
ВЫБРАТЬ
	ПремииЗаАкционныеТоварыДетальная.Продавец,
	СУММА(ПремииЗаАкционныеТоварыДетальная.СуммаПремии) КАК ПремияЗаАкционныйТовар
	
	ПОМЕСТИТЬ ВТ_ПремииЗаАкционныеТовары
ИЗ
	ВТ_ПремииЗаАкционныеТоварыДетальная КАК ПремииЗаАкционныеТоварыДетальная
	
СГРУППИРОВАТЬ ПО
	ПремииЗаАкционныеТоварыДетальная.Продавец

;	
УНИЧТОЖИТЬ ВТ_ПремииЗаАкционныеТоварыДетальная;
		   
// Удержание премии за возврат акционных товаров (детальные записи)
ВЫБРАТЬ
	ОтчетОРозничныхПродажахПродавцы.Продавец,
	ВЫБОР 
		КОГДА УстановкаПремииЗаАкционныеТовары.ВариантРасчетаПремии = ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаСкидокНадбавок.Процентом)
			ТОГДА ВЫРАЗИТЬ(ОтчетОРозничныхПродажахТовары.Сумма*УстановкаПремииЗаАкционныеТовары.Величина/100 КАК ЧИСЛО(15, 2))
		КОГДА УстановкаПремииЗаАкционныеТовары.ВариантРасчетаПремии = ЗНАЧЕНИЕ(Перечисление.ВариантыРасчетаСкидокНадбавок.Суммой)
			ТОГДА УстановкаПремииЗаАкционныеТовары.Величина
		ИНАЧЕ 
			0
	КОНЕЦ КАК УдержаниеПремии
	
	ПОМЕСТИТЬ ВТ_УдержанияПремийЗаВозвратыАкционныхТоваровДетальная
ИЗ
	Документ.ВозвратТоваровОтПокупателя.Товары КАК ВозвратТоваровОтПокупателяТовары
		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ВозвратТоваровОтПокупателя КАК ВозвратТоваровОтПокупателя
			ПО ВозвратТоваровОтПокупателяТовары.Ссылка = ВозвратТоваровОтПокупателя.Ссылка
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
			ПО ВозвратТоваровОтПокупателяТовары.ДокументПартии = ОтчетОРозничныхПродажах.Ссылка
		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.УстановкаПремииЗаАкционныйТовар.Товары КАК УстановкаПремииЗаАкционныеТовары
	   		ПО ВозвратТоваровОтПокупателяТовары.Номенклатура = УстановкаПремииЗаАкционныеТовары.Номенклатура
			И УстановкаПремииЗаАкционныеТовары.Ссылка.Проведен                  
			И ОтчетОРозничныхПродажах.Дата МЕЖДУ УстановкаПремииЗаАкционныеТовары.НачалоАкции И УстановкаПремииЗаАкционныеТовары.ОкончаниеАкции
		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах.Продавцы КАК ОтчетОРозничныхПродажахПродавцы
			ПО ОтчетОРозничныхПродажах.Ссылка = ОтчетОРозничныхПродажахПродавцы.Ссылка
			И ОтчетОРозничныхПродажахПродавцы.ЭтоГлавныйПродавец
		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах.Товары КАК ОтчетОРозничныхПродажахТовары
			ПО ОтчетОРозничныхПродажах.Ссылка = ОтчетОРозничныхПродажахТовары.Ссылка
			И ВозвратТоваровОтПокупателяТовары.Номенклатура = ОтчетОРозничныхПродажахТовары.Номенклатура
ГДЕ
	ВозвратТоваровОтПокупателя.Дата МЕЖДУ НАЧАЛОПЕРИОДА(&ДатаНачала, МЕСЯЦ) И КОНЕЦПЕРИОДА(&ДатаОкончания, МЕСЯЦ)
;	
// Итоговая таблица удержаний премий за возвраты акционных товаров
ВЫБРАТЬ
	УдержанияПремийЗаВозвратыАкционныхТоваров.Продавец,
	СУММА(УдержанияПремийЗаВозвратыАкционныхТоваров.УдержаниеПремии) КАК УдержаниеПремииЗаАкционныйТовар
	
	ПОМЕСТИТЬ ВТ_УдержанияПремийЗаВозвратыАкционныхТоваровИтоговая
ИЗ
	ВТ_УдержанияПремийЗаВозвратыАкционныхТоваровДетальная КАК УдержанияПремийЗаВозвратыАкционныхТоваров
	
СГРУППИРОВАТЬ ПО
	УдержанияПремийЗаВозвратыАкционныхТоваров.Продавец

;	
УНИЧТОЖИТЬ ВТ_УдержанияПремийЗаВозвратыАкционныхТоваровДетальная;

//****************************************************************************************************************
// Расчет премий с групп по %
ВЫБРАТЬ
	ОтчетОРозничныхПродажах.Ссылка,
	ОтчетОРозничныхПродажахТовары.Номенклатура,
	ГруппыПроцентовПоМагазинам.Группа,
	ОтчетОРозничныхПродажах.Склад,
	СУММА(ОтчетОРозничныхПродажахТовары.Сумма) КАК Сумма,
	МАКСИМУМ(ГруппыПроцентовПоМагазинам.Период) КАК ДатаСтавки

	ПОМЕСТИТЬ ВТ_РасчетПремийСГруппПроцентов
ИЗ
	Документ.ОтчетОРозничныхПродажах.Товары КАК ОтчетОРозничныхПродажахТовары
		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах КАК ОтчетОРозничныхПродажах
			ПО ОтчетОРозничныхПродажахТовары.Ссылка = ОтчетОРозничныхПродажах.Ссылка
		//ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставГруппДляПроцентов КАК СоставГруппДляПроцентов
		//	ПО ОтчетОРозничныхПродажахТовары.Номенклатура = СоставГруппДляПроцентов.Номенклатура
		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ГруппыНоменклатуры КАК СоставГруппДляПроцентов
			ПО ОтчетОРозничныхПродажахТовары.Номенклатура = СоставГруппДляПроцентов.Номенклатура
		ПОЛНОЕ СОЕДИНЕНИЕ РегистрСведений.ГруппыПроцентовПоМагазинам КАК ГруппыПроцентовПоМагазинам
			ПО ОтчетОРозничныхПродажах.Склад = ГруппыПроцентовПоМагазинам.Магазин
				И ГруппыПроцентовПоМагазинам.Группа = ЕСТЬNULL(СоставГруппДляПроцентов.Группа, ЗНАЧЕНИЕ(Справочник.ГруппыДляПроцентов.Остальные))
ГДЕ
	ОтчетОРозничныхПродажахТовары.Ссылка В (ВЫБРАТЬ РАЗЛИЧНЫЕ ВТ_ДокументыРабочихСмен.Ссылка ИЗ ВТ_ДокументыРабочихСмен)
	И ГруппыПроцентовПоМагазинам.Период <= ОтчетОРозничныхПродажах.Дата
	
СГРУППИРОВАТЬ ПО
	ОтчетОРозничныхПродажах.Дата,
	ОтчетОРозничныхПродажах.Ссылка,
	ОтчетОРозничныхПродажахТовары.Номенклатура,
	ГруппыПроцентовПоМагазинам.Группа,
	ОтчетОРозничныхПродажах.Склад
	
;
// Таблица сумм и процентов с продаж (по главному продавцу)
ВЫБРАТЬ
	ОтчетОРозничныхПродажахПродавцы.Продавец,
	РасчетПремийСГруппПроцентов.Сумма,
	ГруппыПроцентовПоМагазинам.Процент
	
	ПОМЕСТИТЬ ВТ_СуммыИПроцентыПродажПоГруппам
ИЗ
	ВТ_РасчетПремийСГруппПроцентов КАК РасчетПремийСГруппПроцентов
		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГруппыПроцентовПоМагазинам КАК ГруппыПроцентовПоМагазинам
			ПО РасчетПремийСГруппПроцентов.Группа = ГруппыПроцентовПоМагазинам.Группа
				И РасчетПремийСГруппПроцентов.Склад = ГруппыПроцентовПоМагазинам.Магазин
				И РасчетПремийСГруппПроцентов.ДатаСтавки = ГруппыПроцентовПоМагазинам.Период
		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетОРозничныхПродажах.Продавцы КАК ОтчетОРозничныхПродажахПродавцы
			ПО РасчетПремийСГруппПроцентов.Ссылка = ОтчетОРозничныхПродажахПродавцы.Ссылка
				И ОтчетОРозничныхПродажахПродавцы.ЭтоГлавныйПродавец
				
;
УНИЧТОЖИТЬ ВТ_РасчетПремийСГруппПроцентов;

// Расчет итоговых сумм премий с продаж товаров (с групп по %)
ВЫБРАТЬ
	СуммыИПроцентыПродажПоГруппам.Продавец,
	СУММА(СуммыИПроцентыПродажПоГруппам.Сумма/100*СуммыИПроцентыПродажПоГруппам.Процент) КАК ПремияСГруппПроцентов
	
	ПОМЕСТИТЬ ВТ_ПремииСПродажПоГруппамПроцентов
ИЗ
	ВТ_СуммыИПроцентыПродажПоГруппам КАК СуммыИПроцентыПродажПоГруппам

СГРУППИРОВАТЬ ПО	
	СуммыИПроцентыПродажПоГруппам.Продавец
;
УНИЧТОЖИТЬ ВТ_СуммыИПроцентыПродажПоГруппам;

// Итоговая таблица всех начислений и удержаний (соединяем все итоговые таблицы полученные ранее)
//

// 
ВЫБРАТЬ
	Сотрудники.Продавец,
	ИтогиРезультатовОсновныхНачислений.Оклад,
	ИтогиРезультатовОсновныхНачислений.ОкладСтажера,
	ИтогиРезультатовОсновныхНачислений.ДопСмена,
	ИтогиРезультатовОсновныхНачислений.ДопПремия,
	ИтогиРезультатовОсновныхНачислений.НДФЛ,
	ИтогиДополнительныхНачисленийУдержаний.Премия,
	ИтогиДополнительныхНачисленийУдержаний.Удержания,
	ПремииЗаАкционныеТовары.ПремияЗаАкционныйТовар,
	УдержанияПремийЗаВозвратыАкционныхТоваровИтоговая.УдержаниеПремииЗаАкционныйТовар,
	ПремииСПродажПоГруппамПроцентов.ПремияСГруппПроцентов
ИЗ
	ВТ_Сотрудники КАК Сотрудники
	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИтогиРезультатовОсновныхНачислений КАК ИтогиРезультатовОсновныхНачислений
		ПО Сотрудники.Продавец = ИтогиРезультатовОсновныхНачислений.Продавец
	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ИтогиДополнительныхНачисленийУдержаний КАК ИтогиДополнительныхНачисленийУдержаний
		ПО Сотрудники.Продавец = ИтогиДополнительныхНачисленийУдержаний.Сотрудник
	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПремииЗаАкционныеТовары КАК ПремииЗаАкционныеТовары
		ПО Сотрудники.Продавец = ПремииЗаАкционныеТовары.Продавец
	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_УдержанияПремийЗаВозвратыАкционныхТоваровИтоговая КАК УдержанияПремийЗаВозвратыАкционныхТоваровИтоговая                
		ПО Сотрудники.Продавец = УдержанияПремийЗаВозвратыАкционныхТоваровИтоговая.Продавец
	ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПремииСПродажПоГруппамПроцентов КАК ПремииСПродажПоГруппамПроцентов
		ПО Сотрудники.Продавец = ПремииСПродажПоГруппамПроцентов.Продавец