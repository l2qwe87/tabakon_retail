// Заполняет реквизиты значениями по умолчанию
//
// Параметры: 
//  ПараметрОбъектКопирования	- содержкит ссылку на документ копирования в случае, 
//								  если новый документ создается копированием.
//  ПараметрОснование			- содержкит ссылку на документ-основание в случае, 
//								  если новый документ создается на основании другого.
//
Процедура ИнициализироватьНовыйДокумент(ПараметрОбъектКопирования, ПараметрОснование) Экспорт

	#Если Клиент Или ВнешнееСоединение Тогда
	ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект, глЗначениеПеременной("глТекущийПользователь"), , , ПараметрОбъектКопирования, ПараметрОснование);
	#КонецЕсли

КонецПроцедуры // ИнициализироватьНовыйДокумент()	

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПриемНаРаботу") Тогда
		// Заполнение шапки
		Организация = ДанныеЗаполнения.Организация;
		Сотрудник = ДанныеЗаполнения.Сотрудник;
		
		Для Каждого ТекСтрокаОсновныеНачисления Из ДанныеЗаполнения.ОсновныеНачисления Цикл
			НоваяСтрока = ОсновныеНачисления.Добавить();
			НоваяСтрока.ДействуетС = РабочаяДата;
			НоваяСтрока.ВидРасчета = ТекСтрокаОсновныеНачисления.ВидРасчета;
			НоваяСтрока.Сумма = ТекСтрокаОсновныеНачисления.Сумма;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

// Процедура заполняет ТЧ "ОсновныеНачисления" всеми имеющимися у сотрудниками видами расчетов,
// актуальными на текущую дату
Процедура ЗаполнитьОсновныеНачисленияСотрудника() Экспорт
	ОсновныеНачисления.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СтавкиОсновныхВидовРасчетовСотрудниковСрезПоследних.Сумма,
		|	СтавкиОсновныхВидовРасчетовСотрудниковСрезПоследних.ВидРасчета
		|ИЗ
		|	РегистрСведений.СтавкиОсновныхВидовРасчетовСотрудников.СрезПоследних КАК СтавкиОсновныхВидовРасчетовСотрудниковСрезПоследних
		|ГДЕ
		|   СтавкиОсновныхВидовРасчетовСотрудниковСрезПоследних.Сотрудник = &Сотрудник
		|   И СтавкиОсновныхВидовРасчетовСотрудниковСрезПоследних.Сумма <> 0
		|";
		
	Запрос.УстановитьПараметр("Сотрудник", Сотрудник);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			НоваяСтрока = ОсновныеНачисления.Добавить();
			НоваяСтрока.ДействуетС = Дата;
			НоваяСтрока.ВидРасчета = ВыборкаДетальныеЗаписи.ВидРасчета;
			НоваяСтрока.Сумма = ВыборкаДетальныеЗаписи.Сумма;
		КонецЦикла;
	КонецЕсли;	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	// регистр СтавкиОсновныхВидовРасчетовСотрудников
	Движения.СтавкиОсновныхВидовРасчетовСотрудников.Записывать = Истина;
	Движения.СтавкиОсновныхВидовРасчетовСотрудников.Очистить();
	
	Для Каждого ТекСтрокаОсновныеНачисления Из ОсновныеНачисления Цикл
		Движение = Движения.СтавкиОсновныхВидовРасчетовСотрудников.Добавить();
		Движение.Период = ТекСтрокаОсновныеНачисления.ДействуетС;
		Движение.Сотрудник = Сотрудник;
		Движение.ВидРасчета = ТекСтрокаОсновныеНачисления.ВидРасчета;
		Движение.Сумма = ТекСтрокаОсновныеНачисления.Сумма;
	КонецЦикла;

КонецПроцедуры
